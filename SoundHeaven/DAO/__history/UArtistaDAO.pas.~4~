unit UArtistaDAO;

interface

uses System.SysUtils, Data.Win.ADODB, UConexao, UArtistas;

type
  TArtistaDAO = class
  private
    FConn: TConexaoDB; // Conexão com o SQL Server
  public
    constructor Create;
    destructor Destroy; override;

    // Métodos CRUD
    procedure Inserir(Artista: TArtista);
    procedure Atualizar(Artista: TArtista);
    function BuscarPorId(ID: Integer): TArtista;
    function BuscarPorNome(ArtistaNome: string): TArtista;
    function ListarAtivos: TADOQuery;
  end;

implementation

{ TArtistasDAO }

constructor TArtistaDAO.Create;
begin
  FConn := TConexaoDB.Create; // Instanciar a conexão ADO
end;

destructor TArtistaDAO.Destroy;
begin
  FConn.Free;  // Liberar a conexão ao destruir
  inherited;
end;

// Método para inserir novo artista no banco
procedure TArtistaDAO.Inserir(Artista: TArtista);
var
  Query: TADOQuery;
begin
  Query := TADOQuery.Create(nil);
  try
    Query.Connection := FConn.GetConnection;
    Query.SQL.Text := 'INSERT INTO TB_Artistas (Artista_Nome, Artista_Status, Data_Inclusao) ' +
                      'VALUES (:Artista, :Status, :DataInclusao)';
    Query.Parameters.ParamByName('Artista').Value := Artista.Nome;
    Query.Parameters.ParamByName('Status').Value := Artista.Status;
    Query.Parameters.ParamByName('DataInclusao').Value := Artista.DataInclusão;
    Query.ExecSQL;
  finally
    Query.Free;
  end;
end;

// Método para atualizar artista existente
procedure TArtistaDAO.Atualizar(Artista: TArtista);
var
  Query: TADOQuery;
begin
  Query := TADOQuery.Create(nil);
  try
    Query.Connection := FConn.GetConnection;
    Query.SQL.Text := 'UPDATE TB_Artistas SET Artista_Nome = :Artista, Artista_Status = :Status ' +
                      'WHERE Artista_ID = :ID';
    Query.Parameters.ParamByName('Artista').Value := Artista.Nome;
    Query.Parameters.ParamByName('Status').Value := Artista.Status;
    Query.Parameters.ParamByName('ID').Value := Artista.ID;
    Query.ExecSQL;
  finally
    Query.Free;
  end;
end;

// Método para buscar artista por ID
function TArtistaDAO.BuscarPorId(ID: Integer): TArtista;
var
  Query: TADOQuery;
  Artista: TArtista;
begin
  Query := TADOQuery.Create(nil);
  try
    Query.Connection := FConn.GetConnection;
    Query.SQL.Text := 'SELECT * FROM TB_Artistas WHERE Artista_ID = :ID';
    Query.Parameters.ParamByName('ID').Value := ID;
    Query.Open;

    if not Query.IsEmpty then
    begin
      Artista := TArtista.Create(Query.FieldByName('Artista_Nome').AsString,
                                 Query.FieldByName('Artista_Status').AsInteger);
      Artista.ID := Query.FieldByName('Artista_ID').AsInteger;
      Artista.DataInclusão := Query.FieldByName('Data_Inclusao').AsDateTime;
      Result := Artista;
    end
    else
      Result := nil;
  finally
    Query.Free;
  end;
end;

function TArtistaDAO.BuscarPorNome(ArtistaNome: string): TArtista;
var
  Query: TADOQuery;
  Artista: TArtista;
begin
  Query := TADOQuery.Create(nil);
  try
    Query.Connection := FConn.GetConnection;
    Query.SQL.Text := 'SELECT * FROM TB_Artistas WHERE Artista_Nome = :Artista';
    Query.Parameters.ParamByName('Artista').Value := ArtistaNome;
    Query.Open;

    if not Query.IsEmpty then
    begin
      Artista := TArtista.Create(Query.FieldByName('Artista_Nome').AsString,
                                 Query.FieldByName('Artista_Status').AsInteger);
      Artista.ID := Query.FieldByName('Artista_ID').AsInteger;
      Artista.DataInclusão := Query.FieldByName('Data_Inclusao').AsDateTime;
      Result := Artista;
    end
    else
      Result := nil;
  finally
    Query.Free;
  end;
end;

// Método para listar artistas ativos
function TArtistaDAO.ListarAtivos: TADOQuery;
var
  Query: TADOQuery;
begin
  Query := TADOQuery.Create(nil);
  try
    Query.Connection := FConn.GetConnection;
    Query.SQL.Text := 'SELECT * FROM TB_Artistas WHERE Status = 1';
    Query.Open;
    Result := Query;
  except
    Query.Free;
    raise;
  end;
end;

end.
